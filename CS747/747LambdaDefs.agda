module 747LambdaDefs where

-- Library

open import Relation.Binary.PropositionalEquality using (_â‰¡_; _â‰¢_; refl)
open import Data.Bool using (T; not; true; false)
open import Data.String using (String; _â‰Ÿ_) 
open import Data.Nat using (â„•; zero; suc)
open import Data.Empty using (âŠ¥; âŠ¥-elim)
open import Relation.Nullary using (Dec; yes; no; Â¬_)
open import Relation.Nullary.Decidable using (âŒŠ_âŒ‹; False; toWitnessFalse)
open import Relation.Nullary.Negation using (Â¬?)

-- copied from 747Isomorphism

infix 0 _â‰ƒ_
record _â‰ƒ_ (A B : Set) : Set where
  constructor mk-â‰ƒ  -- This has been added, not in PLFA
  field
    to   : A â†’ B
    from : B â†’ A
    fromâˆ˜to : âˆ€ (x : A) â†’ from (to x) â‰¡ x
    toâˆ˜from : âˆ€ (y : B) â†’ to (from y) â‰¡ y
open _â‰ƒ_

-- Identifiers are strings (for familiarity; later, a better choice).

Id : Set
Id = String

-- Precedence and associativity for our language syntax.

infix  5  Æ›_â‡’_
infix  5  Î¼_â‡’_
infixl 7  _Â·_
infix  8  `suc_
infix  9  `_

-- Syntax of terms.

data Term : Set where
  `_                      :  Id â†’ Term            -- variable
  Æ›_â‡’_                    :  Id â†’ Term â†’ Term     -- lambda (abstraction)
  _Â·_                     :  Term â†’ Term â†’ Term   -- application
  `zero                   :  Term     
  `suc_                   :  Term â†’ Term
  case_[zeroâ‡’_|suc_â‡’_]    :  Term â†’ Term â†’ Id â†’ Term â†’ Term
  Î¼_â‡’_                    :  Id â†’ Term â†’ Term     -- fixpoint for recursion

-- Example expressions.

two : Term
two = `suc `suc `zero

plus : Term
plus = Î¼ "+" â‡’ Æ› "m" â‡’ Æ› "n" â‡’
         case ` "m"
           [zeroâ‡’ ` "n"
           |suc "m" â‡’ `suc (` "+" Â· ` "m" Â· ` "n") ]

2+2 : Term
2+2 = plus Â· two Â· two

-- Examples using Church numerals.
-- These take "interpretations" of suc and zero
-- and can be used as functions without resorting to case.

twoá¶œ : Term
twoá¶œ =  Æ› "s" â‡’ Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z")

-- plusá¶œ can be defined without using fixpoint.

plusá¶œ : Term
plusá¶œ =  Æ› "m" â‡’ Æ› "n" â‡’ Æ› "s" â‡’ Æ› "z" â‡’
         ` "m" Â· ` "s" Â· (` "n" Â· ` "s" Â· ` "z")

sucá¶œ : Term
sucá¶œ = Æ› "n" â‡’ `suc (` "n")

fourá¶œ : Term
fourá¶œ = plusá¶œ Â· twoá¶œ Â· twoá¶œ

-- These definitions let us avoid some backticks and quotes.

Æ›â€²_â‡’_ : Term â†’ Term â†’ Term
Æ›â€² (` x) â‡’ N  =  Æ› x â‡’ N
Æ›â€² _ â‡’ _      =  âŠ¥-elim impossible
  where postulate impossible : âŠ¥

caseâ€²_[zeroâ‡’_|suc_â‡’_] : Term â†’ Term â†’ Term â†’ Term â†’ Term
caseâ€² L [zeroâ‡’ M |suc (` x) â‡’ N ]  =  case L [zeroâ‡’ M |suc x â‡’ N ]
caseâ€² _ [zeroâ‡’ _ |suc _ â‡’ _ ]      =  âŠ¥-elim impossible
  where postulate impossible : âŠ¥

Î¼â€²_â‡’_ : Term â†’ Term â†’ Term
Î¼â€² (` x) â‡’ N  =  Î¼ x â‡’ N
Î¼â€² _ â‡’ _      =  âŠ¥-elim impossible
  where postulate impossible : âŠ¥

-- An example of the use of the new notation.

plusâ€² : Term
plusâ€² = Î¼â€² + â‡’ Æ›â€² m â‡’ Æ›â€² n â‡’
          caseâ€² m
            [zeroâ‡’ n
            |suc m â‡’ `suc (+ Â· m Â· n) ]
  where
  +  =  ` "+"
  m  =  ` "m"
  n  =  ` "n"

-- Values.

data Value : Term â†’ Set where

  V-Æ› : âˆ€ {x N}
      ---------------
    â†’ Value (Æ› x â‡’ N)

  V-zero :
      -----------
      Value `zero

  V-suc : âˆ€ {V}
    â†’ Value V
      --------------
    â†’ Value (`suc V)

-- Substitution is important in defining reduction. 
-- Defined here only for closed terms (simpler).

infix 9 _[_:=_]

_[_:=_] : Term â†’ Id â†’ Term â†’ Term
(` x) [ y := V ] with x â‰Ÿ y
... | yes _          =  V
... | no  _          =  ` x
(Æ› x â‡’ N) [ y := V ] with x â‰Ÿ y
... | yes _          =  Æ› x â‡’ N
... | no  _          =  Æ› x â‡’ N [ y := V ]
(L Â· M) [ y := V ]   =  L [ y := V ] Â· M [ y := V ]
(`zero) [ y := V ]   =  `zero
(`suc M) [ y := V ]  =  `suc M [ y := V ]
(case L [zeroâ‡’ M |suc x â‡’ N ]) [ y := V ] with x â‰Ÿ y
... | yes _          =  case L [ y := V ] [zeroâ‡’ M [ y := V ] |suc x â‡’ N ]
... | no  _          =  case L [ y := V ] [zeroâ‡’ M [ y := V ] |suc x â‡’ N [ y := V ] ]
(Î¼ x â‡’ N) [ y := V ] with x â‰Ÿ y
... | yes _          =  Î¼ x â‡’ N
... | no  _          =  Î¼ x â‡’ N [ y := V ]


-- Single-step reduction (written \em\to).
-- Compatibility rules (descending into subexpressions) written with \xi (Î¾).
-- "Computation here" rules written with \beta (Î²).

infix 4 _â€”â†’_

data _â€”â†’_ : Term â†’ Term â†’ Set where

  Î¾-Â·â‚ : âˆ€ {L Lâ€² M}
    â†’ L â€”â†’ Lâ€²
      -----------------
    â†’ L Â· M â€”â†’ Lâ€² Â· M

  Î¾-Â·â‚‚ : âˆ€ {V M Mâ€²}
    â†’ Value V
    â†’ M â€”â†’ Mâ€²
      -----------------
    â†’ V Â· M â€”â†’ V Â· Mâ€²

  Î²-Æ› : âˆ€ {x N V}
    â†’ Value V
      ------------------------------
    â†’ (Æ› x â‡’ N) Â· V â€”â†’ N [ x := V ]

  Î¾-suc : âˆ€ {M Mâ€²}
    â†’ M â€”â†’ Mâ€²
      ------------------
    â†’ `suc M â€”â†’ `suc Mâ€²

  Î¾-case : âˆ€ {x L Lâ€² M N}
    â†’ L â€”â†’ Lâ€²
      -----------------------------------------------------------------
    â†’ case L [zeroâ‡’ M |suc x â‡’ N ] â€”â†’ case Lâ€² [zeroâ‡’ M |suc x â‡’ N ]

  Î²-zero : âˆ€ {x M N}
      ----------------------------------------
    â†’ case `zero [zeroâ‡’ M |suc x â‡’ N ] â€”â†’ M

  Î²-suc : âˆ€ {x V M N}
    â†’ Value V
      ---------------------------------------------------
    â†’ case `suc V [zeroâ‡’ M |suc x â‡’ N ] â€”â†’ N [ x := V ]

  Î²-Î¼ : âˆ€ {x M}
      ------------------------------
    â†’ Î¼ x â‡’ M â€”â†’ M [ x := Î¼ x â‡’ M ]

infix  2 _â€”â† _

infix  1 begin_
infixr 2 _â€”â†’âŸ¨_âŸ©_
infix  3 _âˆŽ

-- Multistep: the reflexive-transitive closure of single-step.
-- Written \em\rr-.

data _â€”â† _ : Term â†’ Term â†’ Set where
  _âˆŽ : âˆ€ M
      ---------
    â†’ M â€”â†  M

  _â€”â†’âŸ¨_âŸ©_ : âˆ€ L {M N}
    â†’ L â€”â†’ M
    â†’ M â€”â†  N
      ---------
    â†’ L â€”â†  N

begin_ : âˆ€ {M N}
  â†’ M â€”â†  N
    ------
  â†’ M â€”â†  N
begin Mâ€”â† N = Mâ€”â† N

-- Syntax of types.

infixr 7 _â‡’_

data Type : Set where
  _â‡’_ : Type â†’ Type â†’ Type
  `â„• : Type
infixl 5  _,_â¦‚_

-- Contexts provide types for free variables.

data Context : Set where
  âˆ…     : Context
  _,_â¦‚_ : Context â†’ Id â†’ Type â†’ Context

-- The lookup judgment.

infix  4  _âˆ‹_â¦‚_

data _âˆ‹_â¦‚_ : Context â†’ Id â†’ Type â†’ Set where

  Z : âˆ€ {Î“ x A}
      ------------------
    â†’ Î“ , x â¦‚ A âˆ‹ x â¦‚ A

  S : âˆ€ {Î“ x y A B}
    â†’ x â‰¢ y
    â†’ Î“ âˆ‹ x â¦‚ A
      ------------------
    â†’ Î“ , y â¦‚ B âˆ‹ x â¦‚ A
    
-- A "smart" version of S.

Sâ€² : âˆ€ {Î“ x y A B}
   â†’ {xâ‰¢y : False (x â‰Ÿ y)}
   â†’ Î“ âˆ‹ x â¦‚ A
     ------------------
   â†’ Î“ , y â¦‚ B âˆ‹ x â¦‚ A

Sâ€² {xâ‰¢y = xâ‰¢y} x = S (toWitnessFalse xâ‰¢y) x

-- The typing judgment.
-- Intro/elim names in comments.

infix  4  _âŠ¢_â¦‚_

data _âŠ¢_â¦‚_ : Context â†’ Term â†’ Type â†’ Set where

  -- Axiom
  âŠ¢` : âˆ€ {Î“ x A}
    â†’ Î“ âˆ‹ x â¦‚ A
       -------------
    â†’ Î“ âŠ¢ ` x â¦‚ A

  -- â‡’-I
  âŠ¢Æ› : âˆ€ {Î“ x N A B}
    â†’ Î“ , x â¦‚ A âŠ¢ N â¦‚ B
      -------------------
    â†’ Î“ âŠ¢ Æ› x â‡’ N â¦‚ A â‡’ B

  -- â‡’-E
  _Â·_ : âˆ€ {Î“ L M A B}
    â†’ Î“ âŠ¢ L â¦‚ A â‡’ B
    â†’ Î“ âŠ¢ M â¦‚ A
      -------------
    â†’ Î“ âŠ¢ L Â· M â¦‚ B

  -- â„•-Iâ‚
  âŠ¢zero : âˆ€ {Î“}
      --------------
    â†’ Î“ âŠ¢ `zero â¦‚ `â„•

  -- â„•-Iâ‚‚
  âŠ¢suc : âˆ€ {Î“ M}
    â†’ Î“ âŠ¢ M â¦‚ `â„•
      ---------------
    â†’ Î“ âŠ¢ `suc M â¦‚ `â„•

  -- â„•-E
  âŠ¢case : âˆ€ {Î“ L M x N A}
    â†’ Î“ âŠ¢ L â¦‚ `â„•
    â†’ Î“ âŠ¢ M â¦‚ A
    â†’ Î“ , x â¦‚ `â„• âŠ¢ N â¦‚ A
      -------------------------------------
    â†’ Î“ âŠ¢ case L [zeroâ‡’ M |suc x â‡’ N ] â¦‚ A

  âŠ¢Î¼ : âˆ€ {Î“ x M A}
    â†’ Î“ , x â¦‚ A âŠ¢ M â¦‚ A
      -----------------
    â†’ Î“ âŠ¢ Î¼ x â‡’ M â¦‚ A

-- A convenient way of asserting inequality.
-- (Avoids issues with normalizing evidence of negation.)

_â‰ _ : âˆ€ (x y : Id) â†’ x â‰¢ y
x â‰  y  with x â‰Ÿ y
...       | no  xâ‰¢y  =  xâ‰¢y
...       | yes _    =  âŠ¥-elim impossible
  where postulate impossible : âŠ¥

-- A typing derivation for the Church numeral twoá¶œ.
-- Most of this can be done with refining (why not all?).

Ch : Type â†’ Type
Ch A = (A â‡’ A) â‡’ A â‡’ A

âŠ¢twoá¶œ : âˆ€ {Î“ A} â†’ Î“ âŠ¢ twoá¶œ â¦‚ Ch A
âŠ¢twoá¶œ = âŠ¢Æ› (âŠ¢Æ› ((âŠ¢` (Sâ€² Z)) Â· ((âŠ¢` (Sâ€² Z)) Â· (âŠ¢` Z))))

âŠ¢two : âˆ€ {Î“} â†’ Î“ âŠ¢ two â¦‚ `â„•
âŠ¢two = âŠ¢suc (âŠ¢suc âŠ¢zero)

-- A typing derivation for "two plus two".
-- Done in arbitrary contexts to permit reuse.

âŠ¢plus : âˆ€ {Î“} â†’ Î“ âŠ¢ plus â¦‚ `â„• â‡’ `â„• â‡’ `â„•
âŠ¢plus = âŠ¢Î¼ (âŠ¢Æ› (âŠ¢Æ› (âŠ¢case (âŠ¢` (Sâ€² Z)) (âŠ¢` Z) (âŠ¢suc (((âŠ¢` (Sâ€² (Sâ€² (Sâ€² Z)))) Â· (âŠ¢` Z)) Â· (âŠ¢` (Sâ€² Z)))))))

âŠ¢2+2 : âˆ… âŠ¢ plus Â· two Â· two â¦‚ `â„•
âŠ¢2+2 = (âŠ¢plus Â· âŠ¢two) Â· âŠ¢two

âŠ¢plusá¶œ : âˆ€ {Î“ A} â†’ Î“  âŠ¢ plusá¶œ â¦‚ Ch A â‡’ Ch A â‡’ Ch A
âŠ¢plusá¶œ = âŠ¢Æ› (âŠ¢Æ› (âŠ¢Æ› (âŠ¢Æ› (((âŠ¢` (Sâ€² (Sâ€² (Sâ€² Z)))) Â· âŠ¢` (Sâ€² Z)) Â· (((âŠ¢` (Sâ€² (Sâ€² Z))) Â· âŠ¢` (Sâ€² Z)) Â· âŠ¢` Z)))))


-- The rest of the Church examples.

âŠ¢sucá¶œ : âˆ€ {Î“} â†’ Î“ âŠ¢ sucá¶œ â¦‚ `â„• â‡’ `â„•
âŠ¢sucá¶œ = âŠ¢Æ› (âŠ¢suc (âŠ¢` Z))


âŠ¢2+2á¶œ : âˆ… âŠ¢ plusá¶œ Â· twoá¶œ Â· twoá¶œ Â· sucá¶œ Â· `zero â¦‚ `â„•
âŠ¢2+2á¶œ = (((âŠ¢plusá¶œ Â· âŠ¢twoá¶œ) Â· âŠ¢twoá¶œ) Â· âŠ¢sucá¶œ) Â· âŠ¢zero


-- Unicode:

{-
â‡’  U+21D2  RIGHTWARDS DOUBLE ARROW (\=>)
Æ›  U+019B  LATIN SMALL LETTER LAMBDA WITH STROKE (\Gl-)
Â·  U+00B7  MIDDLE DOT (\cdot)
â€”  U+2014  EM DASH (\em)
â†   U+21A0  RIGHTWARDS TWO HEADED ARROW (\rr-)
Î¾  U+03BE  GREEK SMALL LETTER XI (\Gx or \xi)
Î²  U+03B2  GREEK SMALL LETTER BETA (\Gb or \beta)
âˆ‹  U+220B  CONTAINS AS MEMBER (\ni)
âˆ…  U+2205  EMPTY SET (\0)
âŠ¢  U+22A2  RIGHT TACK (\vdash or \|-)
â¦‚  U+2982  Z NOTATION TYPE COLON (\:)
ðŸ˜‡  U+1F607  SMILING FACE WITH HALO
ðŸ˜ˆ  U+1F608  SMILING FACE WITH HORNS

-}
